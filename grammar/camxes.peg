# eberban PEG grammar - v0.31
# ===========================

# GRAMMAR
# main rule, allow language version/dialect annotation
text <- parser_version? text_1
parser_version <- BU_clause (parser_version_short / parser_version_long)
parser_version_long <- spaces? borrowing parser_version_number?
parser_version_short <- parser_version_number
parser_version_number <- spaces? TA+

# main text rule
text_1 <- (free_indicator / free_parenthetical)* paragraphs? spaces? EOF?

# text structure
paragraphs <- paragraph (&PU_clause paragraph)*
paragraph <- PU_clause? sentence (&(PA_clause / PO_clause) sentence)*
sentence <- definition / sentence_scope / sentence_fragments

arguments_list <- (KA_clause / GA_clause)+ PI_clause

definition <- PO_clause definition_key arguments_list? scope POI_clause_elidible
definition_key <- GA_clause / unit_compound / unit_root

sentence_scope <- PA_clause_elidible scope PAI_clause_elidible

sentence_fragments <- PA_clause_elidible fragment+ PAI_clause_elidible
fragment <- DA_clause / FA_clause / VA_clause / SA_clause / ZA_clause

subscope <- arguments_list? definition* scope

# scope
scope <- scope_1 # allow to find only the top-level scopes when using connectives

scope_1 <- scope_connectives / scope_2
scope_connectives <- scope_2 (DA_clause scope_2)+

scope_2 <- scope_group / scope_3
scope_group <- PE_clause scope_1 PEI_clause_elidible

scope_3 <- scope_plural / scope_4
scope_plural <- scope_4 (BA_clause scope_4)+

scope_4 <- sequential

# bindings
sequential <- sequential_neg / sequential_unit sequential?
sequential_neg <- BI_clause sequential_unit+
sequential_unit <- unit explicit_binding?
explicit_binding <- explicit_binding_va explicit_binding_fa* VAI_clause_elidible
explicit_binding_va <- BI_clause? VA_clause subscope 
explicit_binding_fa <- BI_clause? FA_clause subscope

# predicate unit
unit <- (SA_clause / ZA_clause)* unit_1
unit_1 <- quote / variable / unit_borrowing / unit_root / unit_number / unit_compound
unit_root <- free_prefix* spaces? root free_post*
unit_number <- free_prefix* spaces? number free_post*
unit_compound <- free_prefix* spaces? compound free_post*
unit_borrowing <- free_prefix* (spaces? borrowing)+ BE_clause_elidible free_post*

# quotes
quote <- grammatical_quote / one_word_quote / foreign_quote
grammatical_quote <- LA_clause text_1 LAI_clause
one_word_quote <- LE_clause spaces? native_word
foreign_quote <- LO_clause spaces? foreign_quote_open spaces foreign_quote_content foreign_quote_close free_post*
foreign_quote_content <- (foreign_quote_word spaces)*

# number
number <- TA_clause+ BE_clause_elidible

# variables
variable <- MA_clause / BO_clause? KA_clause / BO_clause? GA_clause

# free prefix
free_prefix <- JE_clause / JU_clause

# free suffix
free_post <- JEI_clause / free_indicator / free_parenthetical / free_subscript
free_subscript <- JA_clause number
free_indicator <- CA_clause
free_parenthetical <- JO_clause text_1 JOI_clause

# PARTICLES CLAUSES
BA_clause           <- spaces? BA                               # plural value builder (with)
BE_clause           <- spaces? BE                               # miscellaneous terminator
BE_clause_elidible  <- BE_clause?
BI_clause           <- free_prefix* spaces? BI free_post*       # wide-scope negation
BO_clause           <- spaces? BO                               # variable assignement
BU_clause           <- spaces? BU                               # parser version/dialect
CA_clause           <- free_prefix* spaces? CA                  # free suffix (indicator / marker)
DA_clause           <- free_prefix* spaces? DA free_post*       # logical connectives
FA_clause           <- free_prefix* spaces? FA free_post*       # continue explicit binding
GA_clause           <- free_prefix* spaces? GA free_post*       # pred variables
JA_clause           <- free_prefix* spaces? JA                  # free subscript
JE_clause           <- spaces? JE                               # free scope starter
JEI_clause          <- spaces? JEI                              # free scope termiator
JO_clause           <- free_prefix* spaces? JO                  # free parenthetical started (text)
JOI_clause          <- free_prefix* spaces? JOI                 # free parenthetical terminator
JU_clause           <- spaces? JU                               # free prefix / scope starter
KA_clause           <- free_prefix* spaces? KA free_post*       # individual variables
MA_clause           <- free_prefix* spaces? MA free_post*       # intrinsic variables (pronouns, ...)
PA_clause           <- free_prefix* spaces? PA free_post*       # pred scope starter
PA_clause_elidible  <- PA_clause?
PAI_clause          <- free_prefix* spaces? PAI free_post*      # pred scope terminator
PAI_clause_elidible <- PAI_clause?
PE_clause           <- free_prefix* spaces? PE free_post*       # scope grouping starter
PEI_clause          <- free_prefix* spaces? PEI free_post*      # scope grouping terminator
PEI_clause_elidible <- PEI_clause?
PI_clause           <- free_prefix* spaces? PI free_post*       # pred scope arguments terminator
PO_clause           <- spaces? PO                               # definition starter
POI_clause          <- spaces? POI                              # definition terminator
POI_clause_elidible <- POI_clause?
PU_clause           <- free_prefix* spaces? PU free_post*       # paragraph marker
SA_clause           <- free_prefix* spaces? SA free_post*       # place binding tag
TA_clause           <- free_prefix* spaces? TA                  # numbers/digits
VA_clause           <- free_prefix* spaces? VA free_post*       # starts explicit binding clause
VAI_clause          <- free_prefix* spaces? VAI free_post*      # explicit binding clause terminator
VAI_clause_elidible <- VAI_clause?
LA_clause           <- free_prefix* spaces? LA                  # grammatical quote starter
LAI_clause          <- free_prefix* spaces? LAI free_post*      # grammatical quote terminator
LE_clause           <- free_prefix* spaces? LE                  # one word quote
LO_clause           <- free_prefix* spaces? LO                  # foreign quote
ZA_clause           <- free_prefix* spaces? ZA                  # pred unit transformation

# PARTICLE FAMILIES
BA    <- &particle (b a)
BE    <- &particle (b &e haeiou)
BI    <- &particle (b i)
BO    <- &particle (b o)
BU    <- &particle (b u)
CA    <- &particle (c haeiou)
DA    <- &particle (d aeiou)
FA    <- &particle (f haeiou)
GA    <- &particle (g haeiou)
JA    <- &particle (j a)
JE    <- &particle (j e)
JEI   <- &particle (j e i)
JO    <- &particle (j o)
JOI   <- &particle (j o i)
JU    <- &particle (j &u haeiou)
KA    <- &particle (k haeiou)
MA    <- &particle (m haeiou)
PA    <- &particle (p &a haeiou)
PAI   <- &particle (p a i)
PE    <- &particle (p e)
PEI   <- &particle (p e i)
PI    <- &particle (p i)
PO    <- &particle (p o)
POI   <- &particle (p o i)
PU    <- &particle (p &u haeiou)
SA    <- &particle (s haeiou)
TA    <- &particle (t haeiou) / digit
VA    <- &particle !(VAI &post_word) (v haeiou)
VAI   <- &particle (v a i)
LA    <- &particle !(LAI &post_word) (l &a haeiou)
LAI   <- &particle (l a i)
LE    <- &particle (l &e haeiou)
LO    <- &particle (l &o haeiou)
ZA    <- &particle (z haeiou)

# MORPHOLOGY
# - Forein text quoting
foreign_quote_open <- native_word
foreign_quote_word <- (!spaces .)+ 
foreign_quote_close <- native_word

# - Compounds
compound <- (compound_2 / compound_3 / compound_4 / compound_n) &post_word
compound_2 <- e compound_word compound_word
compound_3 <- i compound_word compound_word compound_word
compound_4 <- o compound_word compound_word compound_word compound_word
compound_n <- a compound_word (!compound_n_end compound_word)* compound_n_end
compound_n_end <- spaces? a &post_word
compound_word <- spaces? (borrowing / native_word)

# - Foreign words
borrowing <-  u (spaces &u / !u) borrowing_content borrowing_end
borrowing_content <- foreign_word
borrowing_end <- (pause_char / space_char / EOF)
foreign_word <- (initial_consonant_pair / consonant / h)? haeiou (consonant_cluster haeiou)* consonant? consonant?

# - Native words
native_word <- root / particle
particle <- !sonorant particle_1  &post_word
root <- !sonorant (root_1 / root_2 / root_3) &post_word

particle_1 <- consonant haeiou

root_1 <- consonant haeiou (sonorant haeiou)+ sonorant?
root_2 <- consonant haeiou sonorant
root_3 <- initial_consonant_pair haeiou (sonorant haeiou)* sonorant?

# - Legal clusters
haeiou <- aeiou+ (h aeiou+)*
aeiou <- a / e / i / o / u

consonant_cluster <- (!(sonorant sonorant sonorant) consonant consonant? consonant? !consonant)
initial_consonant_pair <- &initial consonant consonant !consonant
initial <- (plosib / sibilant? other? liquid?) !consonant

plosib <- plosive sibilant
consonant <- (voiced / unvoiced / liquid / m / n) 
liquid <- (l / r) 
other <- (p / t / k / f / b / d / g / v / m / n !liquid)
plosive <- (t / d / k / g / p / b)
sibilant <- (c / s / j / z) 
# sonorant <- (l / n / r) # technically "alveolar sonorant" as "m" is not included
sonorant <- (n / r)
voiced <- (b / d / g / j / v / z) 
unvoiced <- (c / f / k / p / s / t) 

# Legal letters
a <- [aA] !a # <LEAF>
e <- [eE] !e # <LEAF>
i <- [iI] !i # <LEAF>
o <- [oO] !o # <LEAF>
u <- [uU] !u # <LEAF>

h <- [hH] !h # <LEAF>
n <- [nN] !n # <LEAF>
r <- [rR] !r # <LEAF>

b <- [bB] !b !unvoiced # <LEAF>
d <- [dD] !d !unvoiced # <LEAF>
g <- [gG] !g !unvoiced # <LEAF>
v <- [vV] !v !unvoiced # <LEAF>
j <- [jJ] !j !z !unvoiced # <LEAF>
z <- [zZ] !z !j !unvoiced # <LEAF>
s <- [sS] !s !c !voiced # <LEAF>
c <- [cC] !c !s !voiced # <LEAF>
k <- [kK] !k !voiced # <LEAF>
f <- [fF] !f !voiced # <LEAF>
p <- [pP] !p !voiced # <LEAF>
t <- [tT] !t !voiced # <LEAF>
m <- [mM] !m # <LEAF>
l <- [lL] !l # <LEAF>

# - Spaces / Pause
post_word <- (pause_char &aeiou) / !sonorant &consonant / spaces
spaces <- space_char+ (pause_char &aeiou)? / pause_char &aeiou / EOF


# - Special characters
pause_char <- (['.]) !pause_char
space_char <- [\t\n\r?!\u0020]
digit <- [.0123456789] # <LEAF2>
EOF <- !.